@dc:title "Mission Vocabulary Tutorial"
@dc:creator "ModelWare"
@dc:rights "Copyright 2021, by ModelWare"
vocabulary <http://opencaesar.io/tutorial/vocabulary/mission> with # as mission {
	
	extends <http://opencaesar.io/tutorial/vocabulary/base>
	
 	@rdfs:label "Objective"
	@dc:^description """An [=Objective=] represents a specific interest 
 		that one or more stakeholders have in the successful execution of a mission."""
	concept Objective :> base:IdentifiedIndividual, base:AggregatedIndividual [
		restricts all relation base:aggregates to Objective
		restricts all relation base:isAggregatedIn to Objective
	]

	@rdfs:label "Mission"
	@dc:^description """A [=Mission=] [=pursue=]s [=Objective=]s."""
	concept Mission :> base:IdentifiedIndividual
	
    @rdfs:label "Pursues"
    @dc:^description """A [=Mission=] [=pursues=] zero or more [=Objective=]s."""
    relation entity Pursues [
		from Mission
		to Objective
		@rdfs:label "pursues"
		forward pursues
		@rdfs:label "is pursued by"
		reverse isPursuedBy
		asymmetric
		irreflexive
	]
}

